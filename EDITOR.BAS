10 REM ************************ 
20 REM * EDITOR DE CARACTERES * 
30 REM * POR: Lindomar        *
40 REM * INICIO: 27/10/1993   * 
50 REM * TERMINO: 27/06/1995  * 
60 REM ************************ 
100 '
110 REM *********************** 
120 REM * DEFINICAO DA SCREEN * 
130 REM *********************** 
140 SCREEN 1: WIDTH 32: ON KEY GOSUB 640,750,940,1100,1010,1760: KEY(1) ON: KEY(2) ON: KEY(3) ON: KEY(4) ON: KEY(5) ON: KEY(6) ON 
150 '
160 REM ********************* 
170 REM * DEFINE CARACTERES * 
180 REM ********************* 
190 FOR F=0 TO 15: READ A: VPOKE(48*8) + F, A: NEXT F: DATA 001, 001, 001, 001, 001, 001, 001, 255, 254, 254, 254, 254, 254, 254, 254, 0 
200 FOR F=0 TO 7: READ A: VPOKE(0) + F, A: VPOKE&H800+(0) + F, A: VPOKE&H1000+(0) + F, A: NEXT F: FOR F=0 TO 7: READ A: A(F) = A: NEXT: DATA 254, 254, 254, 254, 254, 254, 254, 0, 241, 225, 225, 225, 225, 255, 81 
210 FOR F=0 TO 15: VPOKE&H2000 + (48*8) + F, 241: NEXT F
215 DIM ARRAY(7)
220 FOR F=0 TO 5: FOR G=0 TO 7: VPOKE&H2000+F*8+G, ARRAY(G): VPOKE&H2800+F*8+G, ARRAY(G): VPOKE&H3000+F*8+G, ARRAY(G): NEXT G: NEXT F 
230 '
240 REM ********************* 
250 REM * DEFINE SPRITE     *
260 REM ********************* 
270 DIM A$(1): DIM X2(7)
271 FOR F=0 TO 7: READ VL: A$(1)=A$(1)+CHR$(VL): NEXT F
272 SPRITE$(1)=A$(1)
273 FOR F=0 TO 7: READ VL: X2(F)=VL: NEXT F
274 DATA 0, 0, 24, 24, 0, 0, 0, 0, 128, 64, 32, 16, 8, 4, 2, 1
280 '
290 REM ******************* 
300 REM * DEFINE A MATRIZ 
310 REM * C=COLUNA 
320 REM * L=LINHA 
330 REM ******************* 
340 CLS: W1=0: W2=0: W3=8: W4=28: W5=20: GOSUB 1500: GOSUB 1450: GOSUB 1190 
350 LOCATE 1,1: PRINT" QUANTAS COLUNAS E LINHAS": LOCATE 1,2: PRINT" TERA A MATRIZ?": LOCATE 1, 3: PRINT" COL( 1 A 18)": LOCATE 1, 4: PRINT" LIN( 1 A 8) ": GOSUB 1430: GOSUB 1490 
360 '
370 REM ************************ 
380 REM * DESENHA A MATRIZ     * 
390 REM * C1=CARACTERE INICIAL • 
400 REM ************************
410 FOR F=0 TO L: LOCATE 9, F: PRINT"|": LOCATE 9 + (C+1), F: PRINT "X": NEXT F: LOCATE 9, L: PRINT"": FOR F=10 TO 10 + C: LOCATE F, L: PRINT"": NEXT F: LOCATE 10 + C, L: PRINT " "
420 LL=97: C1=LL: FOR F=0 TO 7: LOCATE 0,F: PRINT"00000000": NEXT: FOR F=0 TO L-1: FOR G=1 TO C: LOCATE G + 9, F: VPOKEBASE(5) + ((G + 9) + (F*32)), C1: C1 = C1 + 1: NEXT G: NEXT F 
430 GOSUB 1450 
440 '
450 REM -------------------------
460 REM MOVE SPRITE NA TELA 
470 REM X=COL. DO SPRITE  
480 REM L=LIN. DO SPRITE 
490 REM J=STICK(0) J1=MOVE  
500 REM X1 E Y1 = IMPRIME 1 E 0
510 REM -------------------------
520 X = 0: Y = 0: C1 = LL: J1 = 0: J2 = 0: GOSUB 570 
530 J = STICK(0): IF J=1 AND Y > 1 THEN J2=-1 ELSE IF J=3 AND X < 56 THEN J1 = 1 ELSE IF J=5 AND Y < 56 THEN J2=1 ELSE IF J=7 AND X>1 THEN J1=-1 
540 FOR F=1 TO 8: PUT SPRITE 1, (X, Y), 10, 1: X=X+J1: Y=Y+J2: NEXT F: 
541 X1 = X1 + J1: Y1 = Y1 + J2: J1 = 0: J2 = 0: 
542 MM=INP(170): 
543 IF STRIG(0)=-1 AND MM=26 THEN LOCATE X1, Y1: PRINT"1": D=(BY(Y1)) OR (X2(X1)): BY(Y1) = D: VPOKE D1(Y1), D 
550 IF STRIG(0)=-1 AND MM=90 THEN LOCATE X1, Y1: PRINT"0": D=(BY(Y1)) XOR (X2(X1)): BY(Y1) = D: VPOKE D1(Y1), D 
560 GOTO 530 
570 '
580 REM ********************** 
590 REM * IMPRIME NA MATRIZ 
600 REM * CARACTER ESCOLHIDO * 
610 REM ********************** 
620 A=0: FOR F=(C1*8) TO (C1*8) + 7
630 D1(A)=F: BY(A)=VPEEK(F): LOCATE 0, A: PRINT RIGHT$("00000000" + BIN$(BY(A)), 8): A=A+1: NEXT F:RETURN 
640 '
650 REM *********************** 
660 REM * ESCOLHE O CARACTERE * 
670 REM * PARA SER MODIFICADO * 
680 REM *                     *
690 REM *********************** 
700 GOSUB 1490: GOSUB 1450: W1=37: W2=0: W3=3: W4=20: W5=10: GOSUB 1190 
710 C1=LL: LOCATE 1,20: PRINT" CARACTERE =>" 
720 LOCATE 15, 20: PRINT CHR$(C1): IF STICK(0)=3 AND C1 < LL + 143 THEN C1 = C1 + 1 ELSE IF STICK(0) = 7 AND C1 > LL THEN C1 = C1 - 1
730 IF STRIG(0) =-1 THEN GOSUB 1490: GOSUB 570: RETURN ELSE IF INKEY$=CHR$(13) THEN GOSUB 1490: RETURN 530 
740 FOR G=1 TO 50: NEXT: GOTO 720 
750 '
760 REM **********************
770 REM * COR DOS CARACTERES 
780 REM ********************** 
790 GOSUB 1490: GOSUB 1450: W1=37: W2=0: W3=3: W4=20: W5=10: GOSUB 1190 
800 C1=LL: LOCATE 1, 20: PRINT" CARACTERE <=  =>"
810 VPOKEBASE(5) + 15 + (20*32), C1: IF STICK(0) = 3 AND C1 < LL + 143 THEN C1=C1+1 ELSE IF STICK(0) = 7 AND C1> LL THEN C1=C1-1
820 IF STRIG(0) = -1 THEN GOSUB 1490: GOSUB 570: GOSUB 840: RETURN 
830 GOTO 810 
840 '
850 REM ********************* 
860 REM * SUB-ROTINA DE COR 
870 REM ********************* 
880 GOSUB 1490: GOSUB 1450: W1=6: W2=8: W3=19: W4=19: W5=10: GOSUB 1190 
890 LOCATE 10, 7: PRINT" CORES": FOR F=8 TO 15: LOCATE 9, F + 1: PRINT F-7; "- 00" : NEXT
900 CA=C1*8: AA=0: GOSUB 1620
910 FOR F=1 TO 100: AS=INKEY$: NEXT: FOR F=0 TO 7: LOCATE 14, F + 9: CL$="": CC$="": G=1: FOR G=1 TO 2: CL$=INPUT$(1): CC$=CC$ + CL$: LOCATE 14, F+9: PRINT CC$: NEXT G 
920 C=VAL("&H" + CC$): VPOKE&H2000 + CA + F, C: VPOKE&H2800 + CA + F, C: VPOKE&H3000 + CA + F, C: CC$="" : NEXT F 
930 GOSUB 1490: RETURN 
940 '
950 REM *********** 
960 REM * ARQUIVO 
970 REM *********** 
980 GOSUB 1490: GOSUB 1450: W1=9: W2=0: W3=15: W4=20: W5=9: GOSUB 1190 
990 LOCATE 3, 10: PRINT "*** ARQUIVO ***" : LOCATE 3, 12: PRINT"NOME DO ARQUIVO ": GOSUB 1370: N=LL*8: FOR F=50002! TO 51154!: POKE F, VPEEK(N): N = N + 1: NEXT F
1000 N=8968: FOR F= 51155! TO 52307!: POKE F, VPEEK(N): N = N + 1: NEXT: GOSUB 1490: RETURN 
1010 REM ****************
1020 REM * GRAVA        *
1030 REM ****************
1040 GOSUB 1490: GOSUB 1450: W1=9: W2=7: W3=18: W4=26: W5=9: GOSUB 1190: LOCATE 3, 10: PRINT"*** SALVAR ***": LOCATE 3, 12: PRINT"SALVAR ARQUIVO: ": LOCATE 12, 14: PRINT N$: LOCATE 3, 16: PRINT" ESC-NAO RET-SIM"
1050 XX$=INPUT$(1): IF XX$=CHR$(13) THEN 1060 ELSE IF XX$=CHR$(27) THEN GOSUB 1490: RETURN 
1060 GOSUB 1490: GOSUB 1450: W1=0: W2=3: W3=8: W4=22: W5=9: GOSUB 1190: LOCATE 4, 1: PRINT"*** SALVAR ***":LOCATE 4, 3: PRINT"A PARTIR DE: a" : LOCATE 4, 5: PRINT" ATE < a >" : LOCATE 16, 7: PRINT" RET" : AW=97: GOSUB 1640: GOSUB 1490: GOSUB 670
1070 GOSUB 1720: ' BSAVE"BOBBLE0.EDI", 50000!, 51154!, 50000!: BSAVE"BOBBLE1.EDI", 51155!, 52307!, 51155!
1080 GOSUB 1490: RETURN 
1090 GOSUB 1300: RETURN 
1100 '
1110 REM *********** 
1120 REM * CARREGA 
1130 REM *********** 
1140 GOSUB 1490: GOSUB 1450: W1=9: W2=0: W3=15: W4=20: W5=9: GOSUB 1190 
1150 LOCATE 3, 0: PRINT" *** CARREGA ***" : LOCATE 3, 12: PRINT" NOME DO ARQUIVO": GOSUB 1370: BLOAD N$ + "0.EDI" : BLOAD N$ + "1.EDI" 
1160 N=PEEK(50001!)*8: NO=(PEEK(50000!)-98) * 8: FOR F=50002! TO 50002! + NO: VPOKE N, PEEK(F): N=N+1: LOCATE D,91 PRINT F,50002! + NO: NEXT 
1170 NO=PEEK(50001!)*8: N=8192 + NO: N3=(PEEK(50000!)-98) *8: FOR F=51155! TO 51155! + N3: VPOKE N, PEEK(F):N=N +1: LOCATE 0,9: PRINT F, 51155! + N3: NEXT 
1180 GOSUB 1490: RETURN 
1190 '
1200 REM *********** 
1210 REM * WINDOWS * 
1220 REM *********** 
1230 POKE &HE326, W1
1240 POKE &HE327, W2
1250 POKE &HE329, W3 
1260 POKE &HE32A, W4 
1270 POKE &HE3EE, W5
1280 DEF USR=&HE325: U=USR(0) 
1290 RETURN 
1370 REM ********************* 
1380 REM * ENTRA DADOS EM N$ * 
1390 REM ********************* 
1400 N$ = "": LOCATE 3, 14: PRINT"-------": LOCATE 3, 14: FOR F=1 TO 7: N1$=INPUT$(1): N$=N$+N1$: PRINT N1$
1410 IF N1$=CHR$(13) THEN RETURN
1420 NEXT: RETURN 
1430 N$ ="": FOR G = 5 TO 6: LOCATE 1, G: FOR F=1 TO 2: N1$ = INPUT$(1): N$ = N$ + N1$: PRINT N1$: NEXT F 
1440 C(G)=VAL(N$): N$ = "": NEXT G: C=C(5): L=C(6): RETURN 
1450 REM *****************
1460 REM * FECHA WINDOWS * 
1470 REM *****************
1480 POKE &HD000, 0: POKE&HD001, 1: POKE&HD002, 1: POKE&HD003, 1: DEFUSR=&HD004: U=USR(0): RETURN 
1490 POKE &HD000, 1: POKE&HD001, 1: POKE&HD002, 0: POKE&HD003, 1: DEFUSR=&HD004: U=USR(0): GOSUB 620: RETURN 
1500 REM *****************  
1510 FOR F=0 TO 767: VPOKEBASE(5) + F, 0: NEXT
1520 DIM J$(4): J$(1)="◘•••••••○☺" 
1530 J$(2)="♠       ♀♦"
1540 J$(3)="◙♪     ♪♂♦"
1550 J$(4)="☻♣      ♣♥"
1560 FOR G=8 TO 19 STEP 5: FOR F=1 TO 4: LOCATE 1, G+F: PRINT J$(F): NEXT F, G 
1570 LOCATE 2, 10: PRINT"ESCOLHA": LOCATE 4, 15: PRINT"COR": LOCATE 2, 20: PRINT"ARQUIVA" 
1580 FOR G=8 TO 19 STEP 5: FOR F=1 TO 4: LOCATE 21, G+F: PRINT J$(F): NEXT F, G 
1590 LOCATE 22, 10: PRINT"CARREGA": LOCATE 23 ,15: PRINT"GRAVA" : LOCATE 23, 20: PRINT"EDITA" 
1600 LOCATE 12, 11: PRINT"GRADIUS☺": LOCATE 12, 12: PRINT"☻♣     ♥": LOCATE 12, 14: PRINT "EDITOR☺": LOCATE 12, 15: PRINT"         ":LOCATE 14, 17:PRINT" BY☺":LOCATE 14, 18:PRINT"   ":LOCATE 11,20:PRINT"LINDOMAR☺":LOCATE 11, 21: PRINT"          " 
1610 RETURN 
1620 '
1630 FOR F=1 TO 50: CL$="": G=1: NEXT: RETURN 
1640 '
1650 VPOKEBASE(5)+12+(5*32), AW: IF STICK(0)=3 AND AW<LL+143 THEN AW=AW+1 ELSE IF STICK(0)=7 AND AW>LL THEN AW=AW-1 ELSE IF INKEY$=CHR$(13) THEN RETURN
1660 GOTO 1650
1670 '
1680 GOSUB 1490: GOSUB 1450: W1=0: W2=3: W3=8: W4=22: W5=9: GOSUB 1190: LOCATE 4, 1: PRINT"*** SALVAR ***": LOCATE 4, 3: PRINT"CARREGAR A PAR-": LOCATE 4, 5: PRINT"TIR DE < a > ": LOCATE 16 7: PRINT" RET": ES=97 
1690 VPOKEBASE(5) + 12 + (5*32), ES: IF STICK(0)=3 AND ES<200 THEN ES=ES+1 ELSE IF STICK(0)=7 AND ES>0 THEN ES=ES-1 ELSE IF INKEY$=CHR$(13) THEN 1710 
1700 GOTO 1690 
1710 POKE 50000!, AW: POKE 50001!, ES: GOSUB 1490: RETURN 
1720 '** SUB ROTINA "SALVA" ** 
1730 GOSUB 1490: GOSUB 1450: W1=9: W2=7: W3=18: W4=26: W5=9: GOSUB 1190: LOCATE 8, 10: PRINT" *** SALVAR ***" : LOCATE 8, 12: PRINT"SALVANDO ARQUIVO": LOCATE 12, 14: PRINT N$: LOCATE 8, 16: PRINT"ESC='NAO' RET=SIM" 
1740 IF INKEY$=CHR$(13) THEN RETURN ELSE IF INKEY$=CHR$(27) THEN RETURN
1750 GOTO 1740
1760 A=0: FOR F=(C1*8) TO (C1*8)+7 
1770 D1(A)=F: VPOKE D1(A), 0: VPOKE&H2000+F, 241: BY(A)=VPEEK(F):LOCATE 0, A: PRINT RIGHT$("00000000"+ BIN$(BY(A)),8): A=A+1: NEXT F: RETURN 
